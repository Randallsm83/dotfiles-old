" vim:filetype=vim

if empty($MYVIMRC) | let $MYVIMRC = expand('<sfile>:p') | endif

if empty($XDG_CACHE_HOME)  | let $XDG_CACHE_HOME  = $HOME.'/.cache'       | endif
if empty($XDG_CONFIG_HOME) | let $XDG_CONFIG_HOME = $HOME.'/.config'      | endif
if empty($XDG_DATA_HOME)   | let $XDG_DATA_HOME   = $HOME.'/.local/share' | endif
if empty($XDG_STATE_HOME)  | let $XDG_STATE_HOME  = $HOME.'/.local/state' | endif

if !has('nvim')
  set runtimepath^=$XDG_CONFIG_HOME/vim
  set runtimepath+=$XDG_DATA_HOME/vim
  set runtimepath+=$XDG_CONFIG_HOME/vim/after

  set packpath^=$XDG_DATA_HOME/vim,$XDG_CONFIG_HOME/vim
  set packpath+=$XDG_CONFIG_HOME/vim/after,$XDG_DATA_HOME/vim/after

  set backupdir=$XDG_STATE_HOME/vim/backup | call mkdir(&backupdir, 'p', 0700)
  set directory=$XDG_STATE_HOME/vim/swap   | call mkdir(&directory, 'p', 0700)
  set viewdir=$XDG_STATE_HOME/vim/view     | call mkdir(&viewdir,   'p', 0700)
  set undodir=$XDG_STATE_HOME/vim/undo     | call mkdir(&undodir,   'p', 0700)
  set viminfofile=$XDG_STATE_HOME/vim/viminfo
endif

let g:netrw_home = $XDG_DATA_HOME.'/vim'
call mkdir($XDG_DATA_HOME.'/vim/spell', 'p', 0700)
call mkdir($XDG_STATE_HOME.'/vim', 'p', 0700)

" =============================================================================
"                 Vim-Plug Setup
" =============================================================================
" Install vim-plug if not found
if empty(glob($XDG_CONFIG_HOME.'/vim/autoload/plug.vim'))
  silent !curl -fLo $XDG_CONFIG_HOME/vim/autoload/plug.vim --create-dirs
    \ https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
endif

" Automatically run PlugInstall if there are missing plugins
autocmd VimEnter * if len(filter(values(g:plugs), '!isdirectory(v:val.dir)'))
  \| PlugInstall --sync | source $MYVIMRC
\| endif

" Define plugin installation location
call plug#begin($XDG_DATA_HOME.'/vim/plugged')

" ---------- CoC -------------
Plug 'neoclide/coc.nvim', {'branch': 'release'}

" ----- Language Support -----
Plug 'othree/html5.vim'
Plug 'othree/html5-syntax.vim'
Plug 'pangloss/vim-javascript'
Plug 'maxmellon/vim-jsx-pretty'
Plug 'leafgarland/typescript-vim'
Plug 'vim-perl/vim-perl'
Plug 'digitaltoad/vim-pug'

" ----- Code Styling and Formatting -----
Plug 'dense-analysis/ale'
Plug 'prettier/vim-prettier'
Plug 'editorconfig/editorconfig-vim'
Plug 'ntpeters/vim-better-whitespace'

" ----- Editor Enhancements -----
Plug 'tpope/vim-sensible'
Plug 'tpope/vim-surround'
Plug 'scrooloose/nerdcommenter'
Plug 'Raimondi/delimitMate'
Plug 'Yggdroot/indentLine'
Plug 'tpope/vim-unimpaired'

" ----- File Navigation and Management -----
Plug 'scrooloose/nerdtree'
Plug 'ctrlpvim/ctrlp.vim'
Plug 'majutsushi/tagbar'

" ----- Git Integration -----
Plug 'tpope/vim-fugitive'

" ----- Tmux Integration -----
Plug 'christoomey/vim-tmux-navigator'
Plug 'tmux-plugins/vim-tmux'

" ----- Visual Enhancements -----
Plug 'bling/vim-airline'
Plug 'edkolev/tmuxline.vim'

" ----- Colorscheme Choice -----
Plug 'dracula/vim', { 'as': 'dracula' }
Plug 'catppuccin/vim', { 'as': 'catppuccin' }
Plug 'morhetz/gruvbox', { 'as': 'gruvbox' }

call plug#end()

" ============================================
"               Colorscheme Settings
" ============================================

" Set terminal colors
if &term =~ '256color'
  " Enable true (24-bit) colors instead of (8-bit) 256 colors.
  " :h true-color
  if has('termguicolors')
    let &t_8f = "\<Esc>[38;2;%lu;%lu;%lum"
    let &t_8b = "\<Esc>[48;2;%lu;%lu;%lum"
    set termguicolors
  endif

  set background=dark

  " Dracula theme
  " let g:dracula_bold = 1
  " let g:dracula_italic = 1
  " let g:dracula_italic_comment = 1
  " let g:dracula_underline = 1
  " let g:dracula_high_contrast = 1
  " let g:dracula_colorterm = 1
  " autocmd vimenter * ++nested colorscheme dracula

  " Gruvbox theme
  let g:gruvbox_contrast_dark = 'medium'
  autocmd vimenter * ++nested colorscheme gruvbox
endif

" ============================================
"               Language Settings
" ============================================
" Enable filetype plugin and indentation detection
filetype plugin indent on

" Default indentation and tab settings
set tabstop=2               " Set width of a <Tab> to 2 spaces
set shiftwidth=2            " Number of spaces to use for auto-indent
set softtabstop=2           " Number of spaces when <Tab> is pressed
set expandtab               " Convert tabs to spaces globally
set textwidth=120           " Wrap text at 100 characters globally

" Language-specific indentation settings
augroup language_settings
  autocmd!

  " HTML and CSS
  autocmd FileType html,css setlocal ts=2 sw=2 sts=2 et tw=120

  " JavaScript, TypeScript, JSX, Pug
  autocmd FileType jsx,pug,javascript,typescript setlocal ts=2 sw=2 sts=2 et tw=120

  " Perl (4 space width, use tabs, 78 char text width)
  autocmd FileType perl setlocal ts=4 sw=4 sts=4 noet tw=78

  " Python (PEP 8: 4 space width, 79 char text width)
  autocmd FileType python setlocal ts=4 sw=4 sts=4 et tw=79
  autocmd FileType python setlocal formatoptions+=croq
  autocmd FileType python setlocal cinwords=if,elif,else,for,while,try,except,finally,def,class,with

  " Ruby
  autocmd FileType ruby setlocal ts=2 sw=2 sts=2 et tw=120

  " Shell scripts
  autocmd FileType sh setlocal ts=2 sw=2 sts=2 et tw=120

  " Vim script
  autocmd FileType vim setlocal ts=2 sw=2 sts=2 et tw=120

  " JSON (no wrapping as it’s machine-parsed)
  autocmd FileType json setlocal ts=2 sw=2 sts=2 et tw=0

  " Markdown (Wrap at 80 characters for readability, enable spelling)
  autocmd FileType markdown setlocal ts=2 sw=2 sts=2 tw=80 wrap spell

  " YAML (no wrapping for configuration files)
  autocmd FileType yaml setlocal ts=2 sw=2 sts=2 et tw=0

  " Gitconfig (no wrapping for configuration files)
  autocmd FileType gitconfig setlocal ts=4 sw=4 sts=4 noet tw=0
augroup END

" ============================================
"              Custom Functions
" ============================================
" Function to list syntax groups at the cursor and print them
function! Synnames(count) abort
  " Get syntax groups at the cursor using Vim's internal command
  let syn_names = synstack(line('.'), col('.'))

  " Map syntax IDs to their names
  let syn_names = map(syn_names, 'synIDattr(v:val, "name")')

  " If a count is passed, show only the specific group
  if a:count > 0 && a:count <= len(syn_names)
    let selected_name = get(syn_names, a:count - 1, 'No such syntax group')
    echo 'Selected Syntax Group: ' . selected_name
  else
    " Join all syntax group names and echo them
    echo 'All Syntax Groups: ' . join(syn_names, ', ')
  endif
endfunction

" CoC show documentation in preview window
function! s:show_documentation()
  if (index(['vim','help'], &filetype) >= 0)
    execute 'h '.expand('<cword>')
  elseif (coc#rpc#ready())
    call CocActionAsync('doHover')
  else
    execute '!' . &keywordprg . " " . expand('<cword>')
  endif
endfunction

" Function to check for file existence in multiple locations
function! FileReadableInPaths(filename)
  let s:base_paths = ['/ndn', expand('~/ndn'), expand('~/projects/ndn')]
  let s:subdirs = ['', 'etc', 'perl/bin']

  for base in s:base_paths
    for subdir in s:subdirs
      let l:full_path = expand(base . (empty(subdir) ? '' : '/' . subdir) . '/' . a:filename)
      if filereadable(l:full_path)
        return l:full_path
      endif
    endfor
  endfor
  return ''
endfunction

" Function to run ALEFix on a selection range using a temporary buffer
function! ALEFixRange() range
  try
    " Save current window view and position
    let l:winview = winsaveview()

    " Yank selected text to register a
    silent execute a:firstline.','.a:lastline.'yank a'

    " Open a new temporary buffer
    new
    setlocal buftype=nofile bufhidden=hide noswapfile

    " Set filetype same as original buffer
    let &filetype = getbufvar(bufnr('#'), '&filetype')

    " Paste the yanked text
    silent put a

    " Remove the first blank line
    silent 1delete _

    " Run ALEFix on temporary buffer
    ALEFix

    " Wait for ALE to finish
    while ale#engine#IsCheckingBuffer(bufnr('%'))
      sleep 100m
    endwhile

    " Extra wait time for final processing
    sleep 500m

    " Yank the fixed text
    silent %yank a

    " Close temporary buffer and replace selected text
    bdelete!
    silent execute a:firstline.','.a:lastline.'delete _'
    silent execute a:firstline - 1 . 'put a'

    " Restore window view
    call winrestview(l:winview)

  catch
    " Display error if it occurs
    let l:error_message = "Error: " . v:exception
    echohl ErrorMsg
    echom l:error_message
    echohl None
  endtry
endfunction

" ============================================
"             General Editor Settings
" ============================================

" Backup and swap settings
set backup
set swapfile

" UI and Navigation settings
set showcmd              " Show incomplete command in the last line
set mouse=a              " Enable mouse support
set showmatch            " Highlight matching parentheses
set incsearch            " Incremental search, shows matches as you type
set hlsearch             " Highlight all search matches
set splitright           " Split windows to the right
set scrolloff=10         " Keep 10 lines visible above and below the cursor
set wrap                 " Enable line wrapping
set cindent              " Enable C-like indentation
set number               " Show absolute line numbers
set relativenumber       " Show relative line numbers
set ignorecase           " Case-insensitive search
set smartcase            " Case-sensitive if uppercase characters are in search pattern
set wildmenu             " Enable enhanced command-line completion (shows matches in a menu)
set wildignorecase       " Case-insensitive matching in wildmenu
set backspace=indent,eol,start " Allow backspacing over indentation, line breaks, and insert start
set wildmode=longest:full,full " Command-line completion mode

" Other
set ttimeout
set ttimeoutlen=100

" ============================================
"               Key Mappings
" ============================================

" Set mapleader key (using comma as leader key)
let mapleader=','

" <F2> to toggle paste mode
set pastetoggle=<F2>

" <Space> toggle search highlighting and manage highlights while searching
" nnoremap <silent> <Space> :set hlsearch!<CR>
nnoremap <silent> <Space> :call gruvbox#hls_toggle()<CR>
nnoremap * :let @/ = ""<CR>:call gruvbox#hls_show()<CR>*
nnoremap / :let @/ = ""<CR>:call gruvbox#hls_show()<CR>/
nnoremap ? :let @/ = ""<CR>:call gruvbox#hls_show()<CR>?

" Ctrl-N and Ctrl-P to navigate between buffers
nnoremap <C-n> :bnext<CR>
nnoremap <C-p> :bprevious<CR>

" : Shortcut for command-line mode
nnoremap ; :

" j/k navigate wrapped lines
nnoremap j gj
nnoremap k gk
inoremap <up> <c-o>gk
inoremap <down> <c-o>gj

" F1 to Escape
inoremap <F1> <ESC>
nnoremap <F1> <ESC>

" Q for formatting instead of Ex mode
map Q gq

" Disable shift+arrow keys in insert mode
inoremap <S-Up> <nop>
inoremap <S-Down> <nop>

" F4 NERDTree toggle
map <F4> :NERDTreeToggle<CR>

" F3 Tagbar toggle
nnoremap <silent> <F3> :TagbarToggle<CR>

" ,af ALE mapping for fixing
nnoremap <leader>af <Plug>(ale_fix)
vnoremap <Leader>af :call ALEFixRange()<CR>

" Mapping to call Synnames with a count
nnoremap <silent> zS :<C-U>call Synnames(v:count)<CR>

" ----- CoC Mappings -----

" Space-o toggle outline
nnoremap <silent><nowait> <space>o  :call ToggleOutline()<CR>
function! ToggleOutline() abort
  let winid = coc#window#find('cocViewId', 'OUTLINE')
  if winid == -1
    call CocActionAsync('showOutline', 1)
  else
    call coc#window#close(winid)
  endif
endfunction

" Highlight the symbol and its references when holding the cursor
autocmd CursorHold * silent call CocActionAsync('highlight')

" Use <c-space> to trigger completion
inoremap <silent><expr> <C-Space> coc#refresh()

" Use `[g` and `]g` to navigate diagnostics
nmap <silent> [g <Plug>(coc-diagnostic-prev)
nmap <silent> ]g <Plug>(coc-diagnostic-next)

" GoTo code navigation
nmap <silent> gd <Plug>(coc-definition)
nmap <silent> gy <Plug>(coc-type-definition)
nmap <silent> gi <Plug>(coc-implementation)
nmap <silent> gr <Plug>(coc-references)

" Use K to show documentation in preview window
nnoremap <silent> K :call <SID>show_documentation()<CR>

" Symbol renaming
nmap <leader>rn <Plug>(coc-rename)

" Formatting selected code
xmap <leader>f  <Plug>(coc-format-selected)
nmap <leader>f  <Plug>(coc-format-selected)

" ============================================
"             Plugin Configurations
" ============================================

" ALE Configuration
let g:ale_fixers = {
  \ '*': ['remove_trailing_lines', 'trim_whitespace'],
  \ 'sh': ['shfmt'],
  \ 'zsh': ['shfmt'],
  \ 'css': ['prettier'],
  \ 'html': ['prettier'],
  \ 'yaml': ['prettier'],
  \ 'python': ['black'],
  \ 'perl': ['perltidy'],
  \ 'pug': ['puglint', 'eslint'],
  \ 'javascript': ['prettier', 'eslint'],
  \ 'typescript': ['prettier', 'eslint'],
\ }

let g:ale_linters = {
  \ 'sh': ['shellcheck'],
  \ 'zsh': ['zsh-lint'],
  \ 'yaml': ['yaml-language-server'],
  \ 'python': ['flake8'],
  \ 'perl': ['perl', 'perlcritic'],
  \ 'pug': ['puglint', 'eslint'],
  \ 'javascript': ['eslint'],
  \ 'typescript': ['eslint'],
\ }

" Set shfmt options: Indent with 2 spaces, switch-case indentation, simplify redirects
let g:ale_sh_shfmt_options = '-i 2 -ci -sr'

" Set perltidy options
" TODO .perltidy also?
let s:perltidyrc = FileReadableInPaths('perltidyrc')
let g:ale_perl_perltidy_options = s:perltidyrc != '' ? '-pro=' . s:perltidyrc : '-q'

let g:ale_linters_explicit = 1
let g:ale_fix_on_save = 0
let g:ale_lint_on_save = 1
let g:ale_lint_on_enter = 1
let g:ale_lint_on_insert_leave = 1
let g:ale_lint_on_filetype_changed = 1
let g:ale_lint_on_text_changed = 'never'

let g:ale_sign_error = '>>'
let g:ale_sign_warning = '--'
let g:ale_sign_column_always = 1
let g:ale_virtualtext_cursor = 'current'
let g:ale_echo_msg_format = '[%linter%] %s [%severity%]'

let g:ale_completion_enabled = 0
let g:ale_history_log_output = 1

" CoC
let g:coc_global_extensions = [
  \ 'coc-html',
  \ 'coc-css',
  \ 'coc-sh',
  \ 'coc-perl',
  \ 'coc-tsserver',
  \ 'coc-pyright',
  \ 'coc-json',
  \ 'coc-yaml',
  \ 'coc-git',
  \ 'coc-vimlsp',
  \ 'coc-lua',
  \ 'coc-snippets'
\ ]

" Vim-airline
let g:airline_theme='gruvbox'
let g:airline_powerline_fonts = 1
let g:airline_extensions = ['branch', 'coc']
" let g:airline#extensions#ale#open_lnum_symbol = ':L'
" let g:airline#extensions#ale#close_lnum_symbol = ''
" let g:airline#extensions#coc#error_symbol = 'E:'
" let g:airline#extensions#coc#warning_symbol = 'W:'

" Vim-better-whitespace
let g:better_whitespace_enabled=1
let g:better_whitespace_ctermcolor=255
let g:better_whitespace_guicolor='#FFFFFF'
let g:strip_whitespace_on_save=1
let g:strip_whitespace_confirm=0

" JSX Plugin Configuration
let g:jsx_ext_required = 0

" NERDTree Configuration
let NERDTreeShowHidden=1
autocmd bufenter * if (winnr("$") == 1 && exists("b:NERDTree") && b:NERDTree.isTabTree()) | q | endif

" NERDCommenter
let g:NERDSpaceDelims = 1
let g:NERDCommentEmptyLines = 1
let g:NERDTrimTrailingWhitespace = 1

" indentLine
let g:indentLine_char_list = ['|', '¦']
